# Quick Review

## SAS Main procedures

## Data step

```SAS
data rating2;
set ratingdata; /* input dataset */
/* filter - eq is short for equal */
if rating eq 'A';
/* another filter - ne is short for not equal */
if startyr ne 2001;
/* new variable */
length = endyr - startyr;
run;
```

> Use this when there is an input dataset that you want to use, and want to add some variables, and/or filter certain observations.

### Data step with retain

Retain can be used to remember something across the records

```SAS
data sumSale;
set randomSales;
/* retain remembers variables across records */
retain sum;
/* initialization is needed first.year is true if the first record for a year is processed*/
if first.year then sum = 0;
/* update sum -- this is remembered through retain */
sum = sum + sales;
/* last.year is true for the last record in a year */
if last.year then output;
by year; /* repeat for each year, use proc sort to sort the dataset by year first  */
run;
```

> Retain can be useful in combination with 'by'; repeat some logic for some industry, firm, year, etc.

## Proc means

Use proc means for aggregates (mean, median, count). Note that this creates a new dataset with the aggregates (doesn't append these to input dataset).

Typically used with 'by' to get these aggregates for subsamples (by year, by industry, by firm, by industry-year, etc).

For example, if you have 50,000 firmyears over a 10-year period, the output for 'by year' will be 10 records.

> Use this when you need aggreates (mean, median, etc).

## Proc sort

Sorting; keyword 'nodupkey' is helpful to get unique records.

> Use this for sorting (obviously), but also if you want to get rid of duplicates (then first sort such that the observation you want to keep comes first)

Example: you want to have the audit fee for each firm year, but sometimes there are multiple audit firms (when audit firm change), and you want to keep the observation with the highest audit fee. Use two sorts, first sort to get the 'best' record first, then another sort with nodupkey:

```SAS
/* Sort by firm, year and audit fee (highest first) */
proc sort data=dataIn; by gvkey fyear descending auditfee; run;

/* Keep each first firm-year (keeps highest audit fee) */
proc sort data=dataIn nodupkey dupout=dataDropped; by gkvey fyear;run;
```

## Proc rank

Proc rank is helpful to make 'buckets' (below, above median), terciles, quantiles, etc. Note that this works best with continuous variables (sales, assets, return on assets), and less or for categorial variables. (For example, if you have a dummy variable, proc rank won't make be able to make 5 buckets of equal size.)

The ranked variables are appended to the input dataset.

> Use this if you want to have ranked variables (create groups).

## Proc reg

OLS regression. Use ODS output to get coefficient estimates, number of observations, etc. 

> Use proc reg for regressions. With a single regression ODS output works well, with multiple regressions ('by') keep away from ODS (becomes slow).

## Proc export

Proc export is used to export a sas dataset to csv (Excel) or dta (Stata) format.

> Note that if Excel is viewing an csv file, it cannot be exported/overwritten. First close it.

## Proc import

Proc import is good to import Excel sheets (also supports other formats). Make sure row 1 has variable names, and data starts at row 2. (If not, you need to specify other options.)

```SAS
proc import 
  OUT       = work.a_raw DATAFILE= "E:\temp\myExcelSheet.xlsx" 
  DBMS      = xlsx REPLACE;
  SHEET     = "worksheet1"; /* needs to match exactly */
  GETNAMES  = YES;
run;
```

> Proc import preserves the type of variables. So a date in Excel will be the same date in SAS. Do not mix text and numbers in Excel (SAS variables are either text or numeric, not both, so having mixed columns in Excel will give text in SAS.)


## Proc SQL

Proc SQL is very flexible; it can be used as an alternative for the data step (create variables, filter), proc means (compute aggregates, although proc means can do more of these), merging datasets. 

General structure:

- select: which columns to keep, or what to add to the output dataset (like calculations `ni/at as roa`, or aggregate functions 'sum(sale) as sale_total')
- from: source, can be multiple (join)
- where: filters on the input datasets (also used to specifiy how to join datasets)
- group by: grouping, used for aggregate functions (count, sum, max, min, mean, median)
- having: filter on the created dataset

> Use this to join datasets, to add aggregates to datasets, or otherwise add variables. Good replacement for data step and proc means

### group by

The group by is used to aggregate. Example:

```SAS
proc sql;
  create table work.dsOut as select fyear, complex_d, count(*) as numobs
  from work.dsIn group by fyear, complex_d;
quit;
```

### inner join

Matching datasets

```SAS
/* match with Company */
proc sql;
  create table work.b_comp as select a.*, b.fic, b.sic, b.ipodate
  from work.a_funda a, comp.company b
  where a.gvkey = b.gvkey;
quit;
```


### left join

A left join includes records from the left side that have no matches with the right side.

Suppose we have a sample from Compustat and are interested in getting the acquisitions for these firms from SDC. If a firm has made no acquisitions, we still want to keep it in the output dataset.

```SAS
proc sql;
  create table work.dsOut as
  /* a.* means all records from work.dsIn
    b.effdate means select effdate from misc.sdc
  */
  select a.*, b.effdate 
  from work.dsIn a 
  /* perform a left join */
  left join misc.sdc b 
  /* require that gvkey is the same */
  on a.gvkey = b.gvkey 
  /* and that the effdate is in the 365 day period before datadate */
  and a.datadate - 365 <= b.effdate <= a.datadate;
quit;
```

### self join

A self join is a join where one dataset is used for both the left and right side. For example helpful to get lagged variables (example below).

```SAS
proc sql;
  create table work.dsOut as
  	select a.*, a.sale / b.sale - 1 as sales_growth, b.fyear as prev_fyear
  from
    work.dsIn a, work.dsIn b
  where
    a.gvkey = b.gvkey
    /* require that b is one year before 1 (i.e. lagged) */
    and a.fyear -1 = b.fyear; 
quit;
```

### Subqueries

A 'from' does not have to be a dataset, it can be a query also.

Example:

```SAS
proc sql;	
	create table myData2 as
		/* which variables to select: fiscal year and compute market to book */
		select fyear, count(*) as numFirms, median(mtb) as median_mtb
		/* where to get it from: a subquery */
		from (
			select fyear, (csho * prcc_f / ceq) as mtb from comp.funda
			/* filter: get all firms in industry 7370 after 2000 */
			where SICH eq 7370 and fyear > 2000
			/* this is some boilerplate filtering (gets rid of doubles) */
			and indfmt='INDL' and datafmt='STD' and popsrc='D' and consol='C'
			)
		/* compute it for each year => GROUP BY */
		group by fyear;
quit;
```
## Macros

### macro variables

```SAS
/* define a macro variable*/
%let mydata = work.dataIn;
/* using the macro variable */
data dataOut;
set &mydata;
x = 1;run;
```

### proc sql select into

```SAS
/* using proc sql to dynamically set a macro variable */
proc sql; select min(fyear), max(fyear) into :minYr, :maxYr from a_comp;quit;
```

### defining, invoking a macro

```SAS
/* macro definition */
%macro myBestMacro(dsout=, val=); 
  data &dsout;
  x = &val;  
  run;
%mend;

/* invoking of the macro */
%myBestMacro(dsout=myData2, val=5);
```

The SAS macro engine will go through the macro code and replace any instances of '&' and the macro names with the values passed.
In the example above, it means replacing '&dsout' with myData2 and '&val' with 5.

### macro debugging

When invoking a macro, the macro engine first generates SAS codes, which then gets executed. If you want to know which code the macro actually generates, use 'mprint' to write it to disk:

```SAS
filename mprint 'c:\temp\sas_macrocode.txt';
options mfile mprint;
```
To turn it off: `options nomprint nomfile`

Instead of writing to disk, you can also write it to the log with `options mprint;` (to turn off: `options nomprint;`)

### Conditional code and loops

Similar to conditional code/loops in the data step you can use if-then-else and do in macros. In macro code, these are prepended with %, so %if-%then-%else and %do.

#### do, if-then-else in data step 

```SAS
data dsout;
do i = 1 to 9;
	year = 2000 + i;
    if year > 2005 then after2005 = 1;
    else after2005 = 0;
	output;
end;
run;
```

This makes a dataset with 3 variables (i, year and after2005) and 9 records.

#### %do, %if-%then-%else in macro

%macro smallTask (var);
 /* macro if has % */
 %if &var < 5 %then %put smallTask macro says var is pretty small: &var;
 %else %put smallTask macro says var is pretty large: &var;
%mend;

%macro doIt;
    /* assign 1, 2, .. 9 to macro variable i */
    %do i = 1 %to 9;
        /* invoke macro smallTask - yes this is a nested macro call */
        /* note &i, as it needs to be text-replaced */
        %smallTask(&i);
    %end;
%mend;

/* invoke it */
%doIt;

### Macro variables and rsubmit

If you have a remote submit within a macro, by default the macro variables are undefined on the remote server. Use syslput to set macro variables on the remote server before rsubmit as in the following example:

```SAS
/* Making macro variables available on remote server with syslput */

/* login to remote server */
%let wrds = wrds.wharton.upenn.edu 4016;options comamid = TCP remote=WRDS;
signon username=_prompt_;

/* this macro retrieves Compustat Funda variables from year1 to year2 */

%macro getFunda(dsout=, year1=2010, year2=2013, vars=);

/* syslput pushes macro variables to the remote connection */
%syslput dsout = &dsout;
%syslput year1 = &year1;
%syslput year2 = &year2;
%syslput vars = &vars;

rsubmit;

%put Year1 value: &year1 - year2 value: &year2;
%put Collect variables &vars and create &dsout;

data a_funda (keep = gvkey fyear &vars);
set comp.funda;
if &year1 <= fyear <= &year2;
if indfmt='INDL' and datafmt='STD' and popsrc='D' and consol='C' ;
run;
proc download data=a_funda out=&dsout; run;
endrsubmit;

%mend;

/* invoke */
%getFunda(dsout=a_funda1, vars=at sale ceq);
```

## Stata

### Stata commands covered in residency 1

use: to load a dataset

count: counting, can combine with 'if', e.g. count if ni < 0

tab: tabulate

tabstat: descriptive statistics

pwcorr: correlation

reg: regression

winsor: winsorize (needs to be installed first with findit)

sum: summary statistics, get details with ', d', e.g. sum sale, d

sort: sorting

browse: insepct data, can combine with if, e.g., browse if ni < 0



